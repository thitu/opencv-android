buildscript {
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath "com.android.tools.build:gradle:3.1.2"
        classpath "com.jfrog.bintray.gradle:gradle-bintray-plugin:1.7.3"
        classpath "com.github.dcendents:android-maven-gradle-plugin:2.1"
        classpath "org.jfrog.buildinfo:build-info-extractor-gradle:4.7.3"
    }
}

allprojects {
    repositories {
        google()
        jcenter()
    }

    ext.executeCmd = { cmd ->
        def process = cmd.execute()
        def output = new ByteArrayOutputStream()
        process.waitForProcessOutput(output, new ByteArrayOutputStream())

        if (process.exitValue() == 0) {
            output.toString().trim()
        } else {
            println "command: ${cmd} - (${process.exitValue()})"
        }
    }

    ext.environmentValue = { key ->
        System.getenv(key as String)?.trim() ?: null
    }

    ext.currentBranch = {
        environmentValue.call("TRAVIS_BRANCH") ?: executeCmd.call("git rev-parse --abbrev-ref HEAD") ?: "master"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}